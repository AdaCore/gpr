--
--  Copyright (C) 2024, AdaCore
--
--  SPDX-License-Identifier: Apache-2.0 WITH LLVM-Exception
--

with GPR2.Build.Compilation_Unit;
with GPR2.Path_Name;
with GPR2.Project.Registry.Attribute;

with GNATCOLL.OS.Process;

package GPR2.Build.Actions.Ada_Compile.Post_Bind is

   package PRA renames GPR2.Project.Registry.Attribute;

   type Ada_Compile_Post_Bind_Id (<>)
     is new Actions.Ada_Compile.Ada_Compile_Id with private;

   overriding function Image (Self : Ada_Compile_Post_Bind_Id) return String;

   overriding function Db_Filename
     (Self : Ada_Compile_Post_Bind_Id) return Simple_Name;

   type Object is new Actions.Ada_Compile.Object with private;
   --  Action responsible for building Ada sources generated by gnatbind

   Undefined : constant Object;

   overriding function UID (Self : Object) return Actions.Action_Id'Class;

   overriding procedure Initialize
     (Self : in out Object; Src : GPR2.Build.Compilation_Unit.Object);

   overriding function Input_Unit
     (Self : Object) return GPR2.Build.Compilation_Unit.Object;

   overriding procedure On_Tree_Insertion
     (Self     : Object;
      Db       : in out GPR2.Build.Tree_Db.Object;
      Messages : in out GPR2.Log.Object);

   overriding procedure Compute_Signature (Self : in out Object);

   overriding procedure Compute_Command
     (Self : Object;
      Args : out GNATCOLL.OS.Process.Argument_List;
      Env  : out GNATCOLL.OS.Process.Environment_Dict);

private

   type Ada_Compile_Post_Bind_Id (Name_Len : Natural)
     is new Actions.Ada_Compile.Ada_Compile_Id (Name_Len) with null record;

   overriding function Image (Self : Ada_Compile_Post_Bind_Id) return String is
     ("Compile Ada post-bind: " & String (Self.Unit_Name) &
        " (" & String (Self.Ctxt.Path_Name.Simple_Name) & ")");

   overriding function Db_Filename
     (Self : Ada_Compile_Post_Bind_Id) return Simple_Name is
        (Simple_Name ("compile_ada_post_bind_" & To_Lower (Self.Unit_Name)
         & "_" & To_Lower (Self.Ctxt.Name) & ".json"));

   type Object is new Actions.Ada_Compile.Object with record
         Unit : GPR2.Build.Compilation_Unit.Object;
      --  Unit needs to be stored explicitly as it is not contained
      --  by any view. It can not be found with a UID.Ctxt.Unit (...).
   end record;

   overriding function Is_Defined (Self : Object) return Boolean is
     (Self /= Undefined);

   overriding function Input_Unit
     (Self : Object) return GPR2.Build.Compilation_Unit.Object
   is (Self.Unit);

   Undefined : constant Object := (others => <>);
end GPR2.Build.Actions.Ada_Compile.Post_Bind;
